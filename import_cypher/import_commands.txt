
// imported data is on this drive: https://drive.google.com/drive/folders/1n_ZC9UoNQ4eOtR9tJw1NJ_-MFZK4wkSV

// for each csv file:
// 1) save as sheeet
// 2) change permissions to be available for anyone
// 3) File -> Publish to the web -> SELECT Comma-separated values (.csv) -> get link

// reddit.csv -> https://docs.google.com/spreadsheets/d/e/2PACX-1vRdtGwwvfvkdMtjYoX3JYKzw4h0DbwOyPYhNaesM8hn2uEMQx5iLX7yWloAkhMgemfuq7I4vwwRfTLa/pub?output=csv
// reddit_relations.csv -> https://docs.google.com/spreadsheets/d/e/2PACX-1vRIVW0F-kdwsfFJ_8fZHNyuXyiUzPIjmCLwjYP65GhNs5B92Xwglx_LnP69IfVIjJ2WSgm3m_BEbthk/pub?output=csv
// drug_names.csv -> https://docs.google.com/spreadsheets/d/e/2PACX-1vQjIy6aPHe8J0lPHOpfU3-j9_znEPvtnzoWYVWLNXR1jvO2d7hjdg-Tg_Mihmi7pby1kROLyFAUcnBg/pub?output=csv



//////////////////////////////// START CODE TO LOAD DATA ////////////////////////////////
// clear every node
MATCH (n)
DETACH DELETE n;

// create id constraint to avoid duplicates
CREATE CONSTRAINT ON (n:Drug_Names) ASSERT n.id IS UNIQUE;
CREATE CONSTRAINT ON (r:Reddit_Post) ASSERT r.id IS UNIQUE;

// load drug names, create COMMON_AS relation, and create generic name node
LOAD CSV WITH HEADERS FROM "https://docs.google.com/spreadsheets/d/e/2PACX-1vQjIy6aPHe8J0lPHOpfU3-j9_znEPvtnzoWYVWLNXR1jvO2d7hjdg-Tg_Mihmi7pby1kROLyFAUcnBg/pub?output=csv" AS csvLine
MERGE (g:Generic_Names {name: csvLine.full_generic_name})
CREATE (n:Drug_Names {id: toInteger(csvLine.id), name: csvLine.name, name_type: csvLine.name_type})
CREATE (n)-[:COMMON_AS]->(g);

// load reddit data and create author relationship
LOAD CSV WITH HEADERS FROM "https://docs.google.com/spreadsheets/d/e/2PACX-1vRdtGwwvfvkdMtjYoX3JYKzw4h0DbwOyPYhNaesM8hn2uEMQx5iLX7yWloAkhMgemfuq7I4vwwRfTLa/pub?output=csv" AS csvLine
MERGE (a:Author {author: csvLine.author, reddit_id: csvLine.reddit_id}) 
CREATE (r:Reddit_Post {id: toInteger(csvLine.id), created: csvLine.created, title: csvLine.title, link_flair_text: csvLine.link_flair_text, num_comments: csvLine.num_comments, score: csvLine.score, upvote_ratio: csvLine.upvote_ratio})
CREATE (a)-[:REDDIT_AUTHOR]->(r);

// load reddit relations
//:auto USING PERIODIC COMMIT 1000
LOAD CSV WITH HEADERS FROM "https://docs.google.com/spreadsheets/d/e/2PACX-1vRIVW0F-kdwsfFJ_8fZHNyuXyiUzPIjmCLwjYP65GhNs5B92Xwglx_LnP69IfVIjJ2WSgm3m_BEbthk/pub?output=csv" AS csvLine
MATCH (r:Reddit_Post {id: toInteger(csvLine.id_reddit)}),(n:Drug_Names {id: toInteger(csvLine.id_names)})
CREATE (r)-[:REDDIT_Relation {proba: csvLine.proba}]->(n);

// drop indexes added to avoid duplicates
DROP CONSTRAINT ON (n:Drug_Names) ASSERT n.id IS UNIQUE;
DROP CONSTRAINT ON (r:Reddit_Post) ASSERT r.id IS UNIQUE;
//////////////////////////////// END CODE TO LOAD DATA ////////////////////////////////

